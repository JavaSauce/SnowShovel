plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

group = 'net.javasauce'

var buildNumber = System.getenv("BUILD_NUMBER")
version = '0.2'
if (buildNumber != null) {
    version = "$version.$buildNumber"
} else {
    version = "$version-SNAPSHOT"
}

base {
    archivesName = 'SnowShovel'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.covers1624.net/' }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:3.0.0-beta2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:3.0.0-beta2'
    implementation 'org.apache.logging.log4j:log4j-jul:3.0.0-beta2'

    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'

    implementation 'net.covers1624:Quack:0.4.10.117'
    implementation 'net.covers1624:JdkUtils:0.6.0.24'
    implementation 'net.covers1624:curl4j:3.0.16:libcurl'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.0.0.202409031743-r'

    compileOnly 'org.jetbrains:annotations:26.0.1'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'net.javasauce.ss.SnowShovel'
        attributes 'Multi-Release': 'true'
        attributes 'Class-Path': project.configurations.runtimeClasspath.files
                .collect { e -> "libs/${e.getName()}" }
                .join(' ')
        attributes 'Specification-Title': 'SnowShovel'
        attributes 'Specification-Vendor': 'JavaSauce'
        attributes 'Specification-Version': '1'
        attributes 'Implementation-Title': 'SnowShovel'
        attributes 'Implementation-Vendor': 'JavaSauce'
        attributes 'Implementation-Version': version
        attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
    finalizedBy 'signJar'
    from file("LICENSE.txt")
}

tasks.register("makeDist", Zip) {
    from(project.configurations.runtimeClasspath) {
        into 'libs/'
    }
    from jar
}

tasks.register('signJar') {
    dependsOn('jar')

    doFirst {
        def args = [:]
        args['jar'] = jar.archiveFile.get().asFile
        if (System.getenv("KEY_STORE")) {
            args['keyStore'] = System.getenv("KEY_STORE")
            file(System.getenv("KEY_STORE_PROPS")).withReader {
                def props = new Properties()
                props.load(it)
                args.putAll(props)
            }
        } else if (System.getenv("KEY_STORE_FILE")) {
            def tempFile = File.createTempFile("jks", "jks")
            tempFile.bytes = System.getenv("KEY_STORE_FILE").decodeBase64()
            args['keyStore'] = tempFile
            def props = new Properties()
            props.load(new java.io.ByteArrayInputStream(System.getenv("KEY_STORE_PROPS_FILE").decodeBase64()))
            args.putAll(props)
        } else if (project.hasProperty('keyStore')) {
            args['keyStore'] = project.getProperty('keyStore')
            args['alias'] = project.getProperty('keyStoreAlias')
            args['storePass'] = project.getProperty('keyStorePass')
            args['keyPass'] = project.getProperty('keyStoreKeyPass')
        } else {
            println "No signing properties."
            state.setOutcome(org.gradle.api.internal.tasks.TaskExecutionOutcome.UP_TO_DATE)
            didWork = false
            return
        }
        project.ant.invokeMethod('signjar', args)
    }
}

javadoc {
    options.addBooleanOption("Xdoclint:none", true)
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        SnowShovel(MavenPublication) {
            groupId project.group
            artifactId base.archivesName.get()
            version project.version

            from components['java']
            artifact makeDist

            pom {
                name = base.archivesName.get()
                description = base.archivesName.get()
                url = "https://github.com/JavaSauce/${base.archivesName.get()}"
                scm {
                    url = "https://github.com/JavaSauce/${base.archivesName.get()}"
                    connection = "scm:git:git://github.com/JavaSauce/${base.archivesName.get()}.git"
                    connection = "scm:git:git@github.com:JavaSauce/${base.archivesName.get()}.git"
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/JavaSauce/${base.archivesName.get()}/issues"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/JavaSauce/${base.archivesName.get()}/master/LICENSE.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
            }
        }
    }
}

signing {
    if (System.getenv('GPG_PRIVATE_KEY')) {
        useInMemoryPgpKeys(
                new File(System.getenv('GPG_PRIVATE_KEY')).text,
                System.getenv('GPG_PRIVATE_KEY_PASS')
        )
    } else if (System.getenv("GPG_PRIVATE_KEY_FILE")) {
        useInMemoryPgpKeys(
                new String(System.getenv('GPG_PRIVATE_KEY_FILE').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8),
                System.getenv('GPG_PRIVATE_KEY_PASS')
        )
    } else if (project.hasProperty('gpgPrivateKey')) {
        useInMemoryPgpKeys(
                new File(project.getProperty('gpgPrivateKey')).text,
                project.getProperty('gpgPrivateKeyPass')
        )
    } else {
        return
    }
    sign publishing.publications.SnowShovel
}

idea.project.settings {
    delegateActions {
        // Gradle runners are garbage and break frequently.
        delegateBuildRunToGradle = false
        testRunner = org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner.PLATFORM
    }
}
